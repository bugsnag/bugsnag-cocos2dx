buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:4.2.1"
    }
}
apply plugin: "com.android.library"

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16 // Minimum version supported by Cocos2dx
        ndkVersion = "16.1.4479499"

        // manually specify removed BuildConfig.VERSION_NAME field
        // https://developer.android.com/studio/releases/gradle-plugin#version_properties_removed_from_buildconfig_class_in_library_projects
        def versionStr = "0.3.3"
        versionName versionStr
        buildConfigField "String", "VERSION_NAME", "\"$versionStr\""
    }
}

repositories {
    flatDir {
        dirs '../libs'
    }
}

dependencies {
    // Runtime dependency of the bugsnag-plugin-android-* dependencies
    api "org.jetbrains.kotlin:kotlin-stdlib:1.3.61"

    // bugsnag-android libs use this runtime dependency
    implementation "androidx.annotation:annotation:1.1.0"

    runtimeOnly(name:'bugsnag-android', ext:'aar')
    api(name:'bugsnag-android-core', ext:'aar')
    api(name:'bugsnag-plugin-android-ndk', ext:'aar')
    runtimeOnly(name:'bugsnag-plugin-android-anr', ext:'aar')
}

task unpackBuildDeps(type: Copy) {
    description 'Unpacks the library dependencies from zip files into a linkable location'
    from zipTree(file('../libs/bugsnag-plugin-android-ndk.aar'))
    into file("${project.buildDir}/unpacked/bugsnag-plugin-android-ndk")
}

tasks.preBuild.dependsOn(unpackBuildDeps)
